// Package javadoc and sources
java {
    withJavadocJar()
    withSourcesJar()
}

// Configure publishing repositories and publications
publishing {
    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri('https://maven.pkg.github.com/LeakyAbstractions/result')
            credentials {
                username = System.env.GITHUB_USERNAME
                password = System.env.GITHUB_PASSWORD
            }
        }
    }
    publications {
        mavenRelease(MavenPublication) {
            pom {
                groupId         = rootProject.group
                artifactId      = project.artifactId
                name            = project.artifactName
                description     = project.artifactDescription
                version         = rootProject.version
                url             = rootProject.homepage
                licenses {
                    license {
                        name    = rootProject.licenseName
                        url     = rootProject.licenseUrl
                    }
                }
                developers {
                    developer {
                        id      = rootProject.developerId
                        name    = rootProject.developerName
                        email   = rootProject.developerEmail
                    }
                }
            }
            from(components.java)
        }
    }
}

// Configure Bintray publication
bintray {
    user                        = System.env.BINTRAY_USERNAME
    key                         = System.env.BINTRAY_PASSWORD
    publications                = ['mavenRelease']
    publish                     = true
    pkg {
        repo                    = 'maven'
        name                    = project.artifactId
        desc                    = project.artifactDescription
        userOrg                 = 'leakyabstractions'
        licenses                = [ rootProject.licenseId ]
        websiteUrl              = rootProject.homepage
        issueTrackerUrl         = rootProject.issues
        vcsUrl                  = rootProject.repository
        githubRepo              = rootProject.githubId
        githubReleaseNotesFile  = 'README.md'
        labels                  = project.artifactLabels.split(',\\s*')
        publicDownloadNumbers   = true
        version {
            released            = new Date()
            name                = rootProject.version
            vcsTag              = rootProject.version
            desc                = "version ${rootProject.version}"
            attributes = [
                'maturity': project.artifactMaturity
            ]
        }
    }
}
